-- X86 codegeneration interface
-- We use stack machine programs as intermediate representation
import Fun;
import SM;
import Collection;
import List;
import Manifest;
import Buffer;

-- Assembler language interface
-- The registers: 
var regs = ["%ebx", "%ecx", "%esi", "%edi", "%eax", "%edx", "%ebp", "%esp"];

-- We can not freely operate with all register; only with 4 by now
var nRegs = regs.length - 5;

-- For convenience we define the following synonyms for the registers: 
var ebx = R (0),
    ecx = R (1),
    esi = R (2),
    edi = R (3),
    eax = R (4),
    edx = R (5),
    ebp = R (6),
    esp = R (7);

-- We need to know the word size to calculate offsets correctly
var wordSize = 4;

-- We need to distinguish the following operand types: 
--    R (int)       -- hard register                    
--    S (int)       -- a position on the hardware stack 
--    M (string)    -- a named memory location          
--    L (int)       -- an immediate operand
--    I (int, opnd) -- an indirect operand with offset

-- Some x86 instruction (we do not need all of them):
--   Mov   (opnd, opnd)         -- copies a value from the first to the second operand  
--   Lea   (opnd, opnd)         -- loads an address of the first operand into the second
--   Binop (string, opnd, opnd) -- makes a binary operation; note, the first operand   
--                                 designates x86 operator, not the source language one
--   IDiv  (opnd)               -- x86 integer division, see instruction set reference 
--   Cltd                       -- see instruction set reference           
--   Set   (string, string)     -- sets a value from flags; the first operand is the   
--                                 suffix, which determines the value being set, the
--                                 the second --- (sub)register name
--   Jmp   (string)             -- unconditional jump to a label
--   CJmp  (string, string)     -- conditional jump to a label
--   Label (string)             -- a label
--   Push  (opnd)               -- pushes the operand on the hardware stack
--   Pop   (opnd)               -- pops from the hardware stack to the operand
--   Call  (string)             -- calls a function by its name 
--   Ret                        -- returns from a function
--   Meta  (string)             -- metainformation (declarations, etc.)
--   Rishi                      -- (arithmetic) right shift 
                                                               
-- Machine instruction printer
fun insnString (insn) {
  
  fun binopString (op) {
    case op of
      "+"   -> "addl"
    | "-"   -> "subl"
    | "*"   -> "imull"
    | "&&"  -> "andl"
    | "!!"  -> "orl" 
    | "^"   -> "xorl"
    | "cmp" -> "cmpl"
    | _     -> failure("unknown binop")
    esac
  }

  fun opndString (opnd) {
    case opnd of
      R (i)    -> regs [i]
    | S (i)    ->  if i >= 0
                   then sprintf ("-%d(%%ebp)", (i+1) * wordSize)
                   else sprintf ("%d(%%ebp)", 8 + (-1-i) * wordSize)
                   fi

    | M (x)    -> x
    | L (i)    -> sprintf ("$%d", i)
    | I (0, x) -> sprintf ("(%s)", opndString (x))
    | I (n, x) -> sprintf ("%d(%s)", n, opndString (x))
    | _        -> failure("unknown opnd")
    esac
  }

  case insn of
    Cltd               -> "\tcltd\n"
  | Set   (suf, s)     -> sprintf ("\tset%s\t%s\n", suf, s)
  | IDiv  (s1)         -> sprintf ("\tidivl\t%s\n", opndString (s1))
  | Binop (op, s1, s2) -> sprintf ("\t%s\t%s,\t%s\n", binopString (op), opndString (s1), opndString (s2))
  | Lea   (s1, s2)     -> sprintf ("\tleal\t%s,\t%s\n", opndString (s1), opndString (s2))
  | Mov   (s1, s2)     -> sprintf ("\tmovl\t%s,\t%s\n", opndString (s1), opndString (s2))
  | Push  (s)          -> sprintf ("\tpushl\t%s\n", opndString (s))
  | Pop   (s)          -> sprintf ("\tpopl\t%s\n", opndString (s))
  | Ret                -> "\tret\n"
  | Call  (p)          -> sprintf ("\tcall\t%s\n", p)
  | Label (l)          -> sprintf ("%s:\n", l)
  | Jmp   (l)          -> sprintf ("\tjmp\t%s\n", l)
  | CJmp  (c, l)       -> sprintf ("\tj%s\t%s\n", c, l)
  | Meta  (m)          -> m
  | Rishi (s1, s2)     -> sprintf ("\tsarl\t%s,\t%s\n", opndString(s1), opndString(s2))
  | _                  -> failure("unknown box")
  esac
}

-- Environment for symbolic interpreter
-- An environment holds
--    a symbolic stack
--    a maximal stack depth reached so far
--    a set of global variable names
--    a barrier condition (set up right after an unconditional jump is interpreter)
--    a stack map which maps labels to symbolic stacks

fun makeEnv (stack, stackSlots, globals, barrier, stackMap, fLabel, nLocals) {
  -- Returns an internal name for a global variable
  fun globalName (name) {
    "global_" ++ name
  }

  -- Returns a string representation of the environment
  fun envString () {
    sprintf ("Stack     : %s\nStackSlots: %d\nGlobals   : %s\n", stack.string, stackSlots, elements(globals).string)
  }
  
  -- Allocates a new position on the symbolic stack;
  -- returns a pair: a location for allocated item and
  -- an updated environment
  fun allocate () {
    case 
      case stack of
        {}        -> [ebx, 0]
      | S (n) : _ -> [S (n+1), n+2]
      | R (n) : _ -> if n < nRegs then [R (n+1), stackSlots] else [S (nLocals), nLocals + 1] fi
      | _         -> [S (nLocals), nLocals + 1]
      esac
    of [x, n] -> [x, makeEnv (x : stack, if n > stackSlots then n else stackSlots fi, globals, barrier, stackMap, fLabel, nLocals)]esac
  }

  fun allocateOnStack () {
    case 
      case stack of
        {}        -> [S(nLocals), nLocals + 1]
      | S (n) : _ -> [S (n+1), n+2]
      | R (n) : _ -> [S (nLocals), nLocals + 1]
      esac
    of [x, n] -> [x, makeEnv (x : stack, if n > stackSlots then n else stackSlots fi, globals, barrier, stackMap, fLabel, nLocals)]esac
  }


  -- Pushes an item on the symbolic state; returns an updated envirtonment
  fun push (y) {
    makeEnv (y : stack, stackSlots, globals, barrier, stackMap, fLabel, nLocals)
  }

  -- Pops one item from the symbolic stack; returns a pair: a popped
  -- item and an updated environment
  fun pop () {
    case stack of
      x : stack -> [x, makeEnv (stack, stackSlots, globals, barrier, stackMap, fLabel, nLocals)]
    esac
  }

  -- Pops two items from the symbolic stack; returns a triple:
  -- popped items and an undated environment
  fun pop2 () {
    case stack of
      x : y : stack -> [x, y, makeEnv (stack, stackSlots, globals, barrier, stackMap, fLabel, nLocals)]
    esac
  }

  -- Peeks the top of the symbolic stack
  fun peek () {
    fst(stack)
  }
  
  -- Adds a global variable; returns an updated environment
  fun addGlobal (name) {
    makeEnv (stack, stackSlots, addSet (globals, globalName (name)), barrier, stackMap, fLabel, nLocals)
  }

  -- References an allocation
  fun loc (alloc) {
    case alloc of
      Glb (x) -> M (globalName (x))
    | Arg (i) -> S (-1-i)
    | Loc (i) -> S (i)
    esac
  }
  
  -- Gets a list of global variables from the environment
  fun getGlobals () {
    elements(globals)
  }

  -- Gets a maximal stack size from the environment
  fun getStackSize () {
    stackSlots
  }

  -- Gets barrier condition
  fun isBarrier () {
    barrier
  }

  -- Sets barrier condition
  fun setBarrier () {
    makeEnv (stack, stackSlots, globals, true, stackMap, fLabel, nLocals)
  }

  -- Associates the current symbolic state with a label
  fun setStack (l) {
    makeEnv (stack, stackSlots, globals, false, addMap (stackMap, l, stack), fLabel, nLocals)
  }

  -- Retrieves a stack for a label
  fun retrieveStack (l) {
    case findMap (stackMap, l) of
      None         -> makeEnv (stack, stackSlots, globals, false, stackMap, fLabel, nLocals)
    | Some (stack) -> makeEnv (stack, stackSlots, globals, false, stackMap, fLabel, nLocals)
    esac
  }

  -- Sets the label of current function
  fun enterFunction (fLabel, nl) {
    makeEnv (stack, nl, globals, false, stackMap, fLabel, nl)
  }

  -- Gets the label of current function
  fun currentFunction () {
    fLabel
  }

  -- Gets the list of live registers past given symbolic stack depth
  fun liveRegisters (depth) {
    fun inner (d, acc, st) {
      case st of
        {}             -> acc
      | (r@R (_)) : tl -> inner (d+1, if d >= depth then r : acc else acc fi, tl)
      | _         : tl -> inner (d+1, acc, tl)
      esac
    }

    inner (0, {}, stack)
  }

   -- Gets a local static size for the current function
  fun staticSize () {
    stackSlots
  }
  fun getStack() {
    stack
  }

  [envString,
   allocate,
   push,
   pop,
   pop2,
   addGlobal,
   loc,
   getGlobals,
   getStackSize,
   peek,
   isBarrier,
   setBarrier,
   setStack,
   retrieveStack,
   enterFunction,
   currentFunction,
   liveRegisters,
   staticSize,
   allocateOnStack,
   getStack]
}

-- Exported accessors
fun envString (env) {
  env [0] ()
}

fun allocate (env) {
  env [1] ()
}

fun push (env, x) {
  env [2] (x)
}

fun pop (env) {
  env [3] ()
}

fun pop2 (env) {
  env [4] ()
}

fun addGlobal (env, name) {
  env [5] (name)
}

fun loc (env, name) {
  env [6] (name)
}

fun getGlobals (env) {
  env [7] ()
}

fun getStackSize (env) {
  env [8] ()
}

fun peek (env) {
  env [9] ()
}

fun isBarrier (env) {
  env [10] ()
}

fun setBarrier (env) {
  env [11] ()
}

fun setStack (env, l) {
  env [12] (l)
}

fun retrieveStack (env, l) {
  env [13] (l)
}

fun enterFunction (env, fLabel, nL) {
  env [14] (fLabel, nL)
}

fun currentFunction (env) {
  env [15] ()
}

fun liveRegisters (env, depth) {
  env [16] (depth)
}

fun staticSize (env) {
  env [17] ()
}

fun allocateOnStack(env) {
  env[18] ()
}

fun getStack(env) {
  env[19] ()
}

-- Creates an initial environment
fun initEnv () {
  makeEnv ({}, 0, emptySet (compare), false, emptyMap (compare), "", 0)
}

-- Codegeneration helper functions
-- Generates code section
fun codeSection (text) {
  singletonBuffer (Meta ("\t.text\n")) <+> text
}

-- Generates data section
fun dataSection (text) {
  singletonBuffer (Meta ("\t.data\n")) <+> text
}

-- Generates data definition
fun dataDef (name) {
  Meta (sprintf ("%s:\t.int\t0\n", name))
}

-- Generates function prologue
fun prologue (fLabel) {
  singletonBuffer(Label (fLabel)) <+
  Push  (ebp) <+
  Mov   (esp, ebp) <+
  Binop ("-", M (sprintf ("$%s_SIZE", fLabel)), esp)  
}

-- Generates function epilogue
fun epilogue (env) {
  var metaDef = Meta (sprintf ("\t.set\t%s_SIZE,\t%d\n", currentFunction(env), staticSize(env) * wordSize));

  if compare (currentFunction(env), "main") == 0
  then [env, singletonBuffer (Mov (ebp, esp)) <+ Pop (ebp) <+ Binop ("^", eax, eax) <+ Ret <+ metaDef]
  else case pop(env) of
         [y, env] -> [env, singletonBuffer (Mov (y, eax)) <+ Mov (ebp, esp) <+ Pop (ebp) <+ Ret <+ metaDef]
       esac
  fi
}

-- Checks if an operand resides on a stack
fun stackOpnd (opnd) {
  case opnd of
    S (_) -> true
  | _     -> false
  esac      
}

-- Checks if an operand resides in memory
fun memOpnd (opnd) {
  case opnd of
    S (_) -> true
  | M (_) -> true
  | _     -> false
  esac
}

-- Generates a move between locations, using
-- intermediate register if needed
fun move (from, to) {
  if memOpnd (from) && memOpnd (to)
  then singletonBuffer (Mov (from, eax)) <+ Mov (eax, to)
  else singletonBuffer (Mov (from, to))
  fi
}

fun leaMove(from, to) {
  case to of
    R(_) -> singletonBuffer(Lea(from, to))
  | _ -> singletonBuffer(Lea(from, eax)) <+ Mov (eax, to)
  esac
}

-- Gets a suffix for Set instruction from
-- source language comparison operator
fun suffix (op) {
  case op of
    "<"  -> "l"
  | "<=" -> "le"
  | "==" -> "e"
  | "!=" -> "ne"
  | ">=" -> "ge"
  | ">"  -> "g"
  esac
}


fun myprintf(s) {
  skip
}

-- Compiles stack machine code into a list of x86 instructions. Takes an environment
-- and stack machine code, returns an updated environment and x86 code.
fun compile (env, code) {


  fun binopSimple(op, opnd1, opnd2) {
    -- for simple fast operations (+, *, -)
    case opnd1 of
      R(_) -> singletonBuffer(Binop(op, opnd2, opnd1)) |
      _ -> singletonBuffer(Mov(opnd1, eax)) <+ Binop(op, opnd2, eax) <+ Mov(eax, opnd1)
    esac
  }

  fun binopLogic(op, opnd1, opnd2) {
    binopCompare("!=", opnd1, L(0)) <+>
    binopCompare("!=", opnd2, L(0)) <+>
    (case opnd1 of
      R(_) -> singletonBuffer(Binop(op, opnd2, opnd1)) |
      _ -> singletonBuffer(Mov(opnd1, eax)) <+ Binop(op, opnd2, eax) <+ Mov(eax, opnd1)
    esac)
  }

  fun binopDiv(opnd1, opnd2) {
    -- must calculate (opnd1 / opnd2) and store the result in opnd1
    -- so opnd1 must be moved to eax (because this is how idiv works)

    singletonBuffer(Mov(opnd1, eax)) <+ 
    Cltd <+
    IDiv(opnd2) <+ 
    Mov(eax, opnd1)
  }

  fun binopMod(opnd1, opnd2) {
    -- same, but the result was stored in edx
    singletonBuffer(Mov(opnd1, eax)) <+ 
    Cltd <+
    IDiv(opnd2) <+ 
    Mov(edx, opnd1)
  }

  fun binopCompare(op, opnd1, opnd2) {
      fun safeMov(s, d) {
        case s of R(_) -> emptyBuffer() | _ -> singletonBuffer(Mov(s, d)) esac
      }

      var syn1 = case opnd1 of R(_) -> opnd1 | _ -> eax esac,
          syn2 = case opnd2 of R(_) -> opnd2 | _ -> edx esac;
      
      safeMov(opnd1, syn1) <+>
      safeMov(opnd2, syn2) <+
      Binop("cmp", syn2, syn1) <+
      Mov(L(0), eax) <+
      Set(suffix(op), "%al") <+ 
      Mov(eax, opnd1)
  }

  fun cmpZero(opnd) {
    case opnd of
      R(_) -> singletonBuffer(Binop("cmp", L(0), opnd))
    | _ -> singletonBuffer(Mov(opnd, eax)) <+ Binop("cmp", L(0), eax)
    esac
  }

  fun isArithmeticOp(op) {
    case op of
        "+" -> 1
      | "*" -> 1
      | "-" -> 1
      | _ -> 0
    esac
  }

  fun isLogicOp(op) {
    case op of
      "!!" -> 1
    | "&&" -> 1
    | _    -> 0
    esac
  }

  fun peekN(env, n) {
    fun topN(xs, n) {
      case n of
        0 -> {}
      | _ -> 
        case xs of
          x:xs -> x:topN(xs, n - 1)
        esac 
      esac
    }

    reverse(topN(getStack(env), n))
  }

  fun moveN(opnds) {
    var metacode = singletonBuffer(Meta(sprintf("# Moving %d operands on actual hardware\n", size(opnds))));

    fun moveN(opnds) {
      case opnds of
        {} -> emptyBuffer()
      | opnd:opnds -> singletonBuffer(Push(opnd)) <+> moveN(opnds)
      esac
    }
    
    metacode <+> moveN(opnds)
  }

  fun dropN(env, n) {
    case n of
      0 -> env
    | _ -> snd(pop(dropN(env, n - 1)))
    esac
  }

  fun deleteN(n) {
    if n then deleteN(n - 1) <+ Pop(edx) else emptyBuffer() fi
  } 
  fun returnN(opnds) {
    var metacode = singletonBuffer(Meta(sprintf("# Returning %d operands from actual hardware\n", size(opnds))));

    fun returnN(opnds) {
      case opnds of
        {} -> emptyBuffer() 
      | opnd:opnds -> returnN(opnds) <+ Pop(opnd)
      esac
    }
    
    case returnN(opnds) of
      code -> metacode <+> code
    esac
  }

  foldl (
    fun ([env, scode], i) {
      -- scode : yet implemented x86 code
      -- i : new SM instruction to implement


      var code = scode <+ Meta ("# " ++ showSMInsn (i) ++ "\n");
      -- here just adding comments as i understand it

      case i of
        BEGIN (fLab, na, nl) ->
          [enterFunction(env, fLab, nl), code <+> prologue(fLab)]
      | END ->
          case epilogue(env) of
            [env, epilogueCode] -> [env, code <+> epilogueCode]
          esac
      | CALL (fLab, nArgs) -> (
          var liveRegs = liveRegisters(env, nArgs);
          var args = peekN(env, nArgs);

          case allocate(dropN(env, nArgs)) of
            [s, env] -> [env, code <+>
                        moveN(liveRegs) <+>
                        moveN(reverse(args)) <+>
                        singletonBuffer(Call(fLab)) <+> 
                        deleteN(nArgs) <+> 
                        returnN(liveRegs) <+
                        Mov(eax, s)]
          esac
        )
      | GLOBAL (s) -> [addGlobal(env, s), code]
        -- Read : 
      | READ ->
            case allocate(env) of
	            [s, env] -> [env, code <+ Push (edx) <+ Push (ecx) <+ Call ("Lread") <+ Pop (ecx) <+ Pop (edx) <+ Mov (eax, s)]
            esac             
      | WRITE ->
            case pop(env) of
	            [s, env] -> [env, code <+ Push (edx) <+ Push (ecx) <+ Push (s) <+ Call ("Lwrite") <+ Pop (eax) <+ Pop (ecx) <+ Pop (edx)]
            esac
      | LD (x) ->
          case allocate(env) of
            [s, env] -> [env, code <+> move(loc(env, x), s)]
          esac
      | ST (x) ->
          case pop(env) of
            [s, env] -> [env, code <+> move(s, loc(env, x))]
          esac 
      | LDA(addr) ->
          case allocate(env) of
            [s, env] -> [env, code <+> leaMove(loc(env, addr), s)]
          esac
      | STI -> 
          case pop2(env) of
            [s2, s1, env] -> [push(env, s1), code <+> move(s2, I(0, s1)) <+> move(s2, s1)]
          esac
      | DROP -> [snd(pop(env)), code]
      | DUP -> failure("DUP used")
      | CONST(n) ->
          case allocate(env) of
            [s, env] -> [env, code <+> move(L(n), s)]
          esac
      | LABEL(lab) -> [retrieveStack(env, lab), code <+ Label(lab)]
      | JMP(lab) -> [setStack(env, lab), code <+ Jmp(lab)]
      | CJMP(lab) -> case pop(env) of
          [s, env] -> [setStack(env, lab), code <+> cmpZero(s) <+ CJmp("ne", lab)]
                     esac 
      | BINOP(op) ->
        ( case pop2(env) of
            [s2, s1, env] -> [push(env, s1), code <+> (
              if isArithmeticOp(op) then binopSimple(op, s1, s2)
              elif isLogicOp(op) then binopLogic(op, s1, s2)
              else
                case op of
                  "/" -> binopDiv(s1, s2)
                | "%" -> binopMod(s1, s2)
                | _   -> binopCompare(op, s1, s2)
                esac
              fi
            )]
          esac
        )
      esac
    }, [env, emptyBuffer ()], code)
}

-- A top-level codegeneration function. Takes a driver's environment and a stack machine program,
-- compiles the program into machine code, and compiles the machine code into an executable
public fun compileX86 (args, code) {
  case compile (initEnv (), code) of
    [env, code] ->
       var asmFile = args.getBaseName ++ ".s",
             runtime = case getEnv ("LAMA_RUNTIME") of
                         #val -> "../runtime/"
                       | path     -> path
                       esac ++ "/runtime.o";

       --printf(getBuffer(code).string);

       fwrite (asmFile,
               stringcat(
                map (insnString, 
                    getBuffer (
                      singletonBuffer (Meta ("\t.global\tmain\n")) <+>
                      dataSection (listBuffer (map (dataDef, getGlobals(env)))) <+>
                      codeSection (code)
                    )
                   )));
                 
       
       system ({"gcc -g -m32 -o ", args.getBaseName, " ", runtime, " ", asmFile}.stringcat)
  esac
}
