-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var(0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}


fun addVariables(ds, state) {
  case ds of
    {} -> state |
    Var(d):ds -> addVariables(ds, addNames(state, d)) |
    Fun(nm, ag, bd):ds -> addVariables(ds, addFunction(state, nm, ag, bd))
  esac
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun myPrint(s) {
  printf(s)
}

fun zipArgs(state, ags, agsExp) {
  case [ags, agsExp] of
    [{}, {}] -> state
  | [ag:ags, agVal:agsExp] ->
      zipArgs(addName(state, ag, Var(agVal)), ags, agsExp)
  | _ -> failure("different number of args ")
  esac
}

fun eval (c@[state, world], expr) {
  case expr of
    Binop(s, e1, e2) ->
      case evalList(c, {e1, e2}) of 
        [c, {res1, res2}] -> [c, evalOp(s, res1, res2)] 
      esac
  | Read(s) ->
      case readWorld(world) of [i, w] -> [[state <- [s, Var(i)], w], Void] esac
  | Write(e) ->
      case eval(c, e) of
        [[state, world], res] -> [[state, writeWorld(res, world)], Void]
      esac
  | Ignore(e) ->
      case eval(c, e) of [c, _] -> [c, Void] esac
  | Skip -> [c, Void]
  | Ref(s) ->
      [c, Refer(s)]
  | Var(s) ->
      [c, case lookup(state, s) of Var(x) -> x esac]
  | Seq(e1, e2) ->
      case evalList(c, {e1, e2}) of
        [c, {_, res}] -> [c, res]
      esac
  | Call(nm, agsExp) -> (
      case lookup(state, nm) of
        Fun(ags, bd) ->
          case evalList(c, agsExp) of
            [c, agsExp] -> 
              case eval([zipArgs(enterFunction(state), ags, agsExp), world], bd) of
                [[state, world], res] ->
                  [[leaveFunction(fst(c), getGlobal(state)), world], res]
              esac
          esac
      esac
    )
  | Scope(ds, e) ->
      case addVariables(ds, enterScope(state)) of
        state ->
          case eval([state, world], e) of
            [[state, w], res] -> [[leaveScope(state), w], res]
          esac
      esac
  | Const(i) -> [c, i]
  | While(eCond, eStmt) -> (
      case eval(c, eCond) of
        [c, res] -> if res 
                    then eval(eval(c, eStmt).fst, While(eCond, eStmt))
                    else [c, Void]
                    fi
      esac
    )
  | DoWhile(eCond, eStmt) -> (
      case eval(c, eStmt) of
        [c, Void] -> eval(c, While(eCond, eStmt))
      esac
    )
  | If(eCond, e1, e2) -> (
      case eval(c, eCond) of
        [c, res] -> if res
                    then eval(c, e1)
                    else eval(c, e2)
                    fi
      esac
    )
  | Assn(e1, e2) -> 
      case evalList(c, {e1, e2}) of
        [c, {Refer(s), i}] -> [[c.fst <- [s, Var(i)], c.snd], i]
      esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, rs] -> c.snd.getOutput
  esac
}